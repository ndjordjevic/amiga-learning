; $VER: StartHere_script 47.4 (19.03.2021)
; Perform the basic assigns to use programs from the CD-ROM.
; Sets up basic DAControl cache if enough memory available

Assign >nil: EXISTS AmigaOS3.2CD:
If warn
Echo ""
Echo "Error: AmigaOS 3.2 source files MUST be held in a volume named AmigaOS3.2CD"
Wait 5
Quit
EndIF 

Assign C: AmigaOS3.2CD:C ADD
Assign Devs: AmigaOS3.2CD:Devs ADD
Assign Utilities: AmigaOS3.2CD:Utilities ADD

; Here we detect outdated and incompatible CDTV Extended ROMs

Amigamodel >ENV:MACHINE

IF $MACHINE EQ CDTV
Version cdtv.device >ENV:CDTVD 
ENDIF

IF "$CDTVD" EQ "cdtv.device 35.2"
ECHO ""
ECHO " CDTV Extended ROM:*E[32m" "2.9 beta" "*E[0m"
ECHO "*E[32m*E[41m*E[1m" "Please upgrade your CDTV Extended ROM set!" "*E[0m"
QUIT
ENDIF

IF "$CDTVD" EQ "cdtv.device 35.7"
ECHO ""
ECHO " CDTV Extended ROM:*E[32m" "0.1 gamma" "*E[0m"
ECHO "*E[32m*E[41m*E[1m" "Please upgrade your CDTV Extended ROM set!" "*E[0m"
QUIT
ENDIF

IF "$CDTVD" EQ "cdtv.device 35.9"
ECHO ""
ECHO " CDTV Extended ROM:*E[32m" "1.0" "*E[0m"
ECHO "*E[32m*E[41m*E[1m" "Please upgrade your CDTV Extended ROM set!" "*E[0m"
QUIT
ENDIF
  
; If < 1 MB RAM free then show not enough RAM message and quit.

avail >env:MemTotB total
If not val $MemTotB GE 1000000
  RequestChoice "              Virtual floppy disk mounting process suspended" "At least 1 MB of available RAM is required to install AmigaOS 3.2!*n*n                       Aborting procedure..." "Okay" >NIL:
  Quit
EndIf

Assign >nil: EXISTS Install3.2:
If warn
  DAControl EJECT UNIT 0 TIMEOUT 15 >NIL:
     If val $MemTotB GE 2000000
       DAControl enablecache=yes cachesize=1000000 load AmigaOS3.2CD:ADF/Install3.2.adf
       Else
       DAControl load AmigaOS3.2CD:ADF/Install3.2.adf
     EndIf
EndIf

If val $MemTotB GE 2000000
  DAControl enablecache=yes cachesize=1000000 load AmigaOS3.2CD:ADF/Locale.adf device da3:
  Else
  DAControl load AmigaOS3.2CD:ADF/Locale.adf device da3:
EndIf

RequestChoice "                    ATTENTION!" "To start the AmigaOS 3.2 installation procedure*n*ndouble-click on the 'Install3.2' disk icon*n*nlocated in your Workbench window." "OK" >NIL:

quit